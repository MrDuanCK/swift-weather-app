apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.readdle.android.swift'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

swift {
    useKapt true
    cleanEnabled true
    debug {
        extraBuildFlags("-Xswiftc", "-DDEBUG")
    }
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.readdle.weather"
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["com.readdle.codegen.package": """{
                   "moduleName": "WeatherCoreBridgeGenerated",
                   "importPackages": ["AnyCodable", "JavaCoder", "WeatherCore", "WeatherCoreBridge"]
               }
               """
                ]
            }
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "API_KEY", "\""+getApiKey()+"\""
        }
        release {
            debuggable false
            jniDebuggable false
        }
        debug {
            debuggable true
            jniDebuggable true
            ndk {
                abiFilters = ["arm64-v8a", "x86_64"]
            }
        }
    }

}

static def getApiKey(){
    Properties props = new Properties()
    props.load(new FileInputStream(new File('local.properties')))
    return props['API_KEY']
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    kapt "com.readdle.swift.java.codegen:compiler:0.9.3"
    implementation "com.readdle.swift.java.codegen:annotations:0.9.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"
    implementation "androidx.activity:activity-ktx:1.5.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    kapt "com.google.dagger:hilt-android-compiler:2.40.1"
    implementation "com.google.dagger:hilt-android:2.40.1"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.google.android.material:material:1.6.1'
}
