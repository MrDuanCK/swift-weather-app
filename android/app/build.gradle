import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.readdle.android.swift'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

swift {
    useKapt true
    cleanEnabled true
    debug {
        extraBuildFlags("-Xswiftc", "-DDEBUG")
    }
}

android {
    namespace = "com.readdle.weather"

    defaultConfig {
        applicationId "com.readdle.weather"
        minSdkVersion 24
        compileSdk 34
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["com.readdle.codegen.package": """{
                   "moduleName": "WeatherCoreBridgeGenerated",
                   "importPackages": ["AnyCodable", "JavaCoder", "WeatherCore", "WeatherCoreBridge"]
               }
               """
                ]
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "API_KEY", "\"" + openWeatherAPIKey + "\""
        }
        release {
            debuggable false
            jniDebuggable false
        }
        debug {
            debuggable true
            jniDebuggable true
            ndk {
                // Optimize for fast build times by not building all ABIs for debuggable variants.
                //noinspection ChromeOsAbiSupport
                abiFilters = (System.getProperty("os.arch") == "aarch64") ? ["arm64-v8a"] : ["x86_64"]
            }
        }
    }

}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    kapt "com.readdle.swift.java.codegen:compiler:0.9.4"
    kapt "com.google.dagger:hilt-android-compiler:2.44"

    implementation "com.readdle.swift.java.codegen:annotations:0.9.4"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.activity:activity-ktx:1.8.2"
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "com.google.dagger:hilt-android:2.44"
    implementation 'com.google.android.material:material:1.11.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
